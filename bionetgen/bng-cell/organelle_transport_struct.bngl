begin parameters
    Nav 6.022e8 # Avogadroâ€™s number adjust for microns scale
    kp_LR 4e8/ Nav
    km_LR 10
    k_LRtrans 1000
    kp_LT1 4e8/ Nav
    km_LT1 10
    k_LT1trans 1000
    kp_L2T2 4e8/ Nav
    km_L2T2 10
    k_L2T2trans 1000
    vol_Cell 1
    rxn_layer_t 0.01
    vol_O1M 0.11 * rxn_layer_t
    vol_Organelle_1 0.11
    vol_O2M 0.03 * rxn_layer_t
    vol_Organelle_2 0.03
end parameters

begin compartments
  Cell  3  vol_Cell  
  O1M  2  vol_O1M   Cell
  Organelle_1  3  vol_Organelle_1   O1M
  O2M  2  vol_O2M   Cell
  Organelle_2  3  vol_Organelle_2   O2M
end compartments

begin molecule types
L(r,t,mod~0~1)
R(l)
T1(l)
T2(l)
end molecule types

begin seed species
L(r,t,mod~0)@Cell 1000
R(l)@O1M 200
T1(l)@O1M 1000
T2(l)@O2M 1000
end seed species

begin observables
Species FreeL_Cell L(r,t,mod~0)@Cell
Species FreeL_2_Cell L(r,t,mod~1)@Cell
Species FreeL_2_Organelle_2 L(r,t,mod~1)@Organelle_2
Species BoundLR_Cell L(r!+,t,mod~0)@Cell
end observables

begin functions
end functions

begin reaction rules
L(r,t,mod~0)@Cell + R(l)@O1M <-> L(r!1,t,mod~0)@Cell.R(l!1)@O1M kp_LR, km_LR
L(r!1,t,mod~0)@Cell.R(l!1)@O1M -> L(r,t,mod~0)@Organelle_1 + R(l)@O1M k_LRtrans
L(r,t,mod~0)@Organelle_1 + T1(l)@O1M <-> L(r,t!1,mod~0)@Organelle_1.T1(l!1)@O1M kp_LT1, km_LT1
L(r,t!1,mod~0)@Organelle_1.T1(l!1)@O1M -> L(r,t,mod~1)@Cell + T1(l)@O1M   k_LT1trans
L(r,t,mod~1)@Cell + T2(l)@O2M <-> L(r,t!1,mod~1)@Cell.T2(l!1)@O2M kp_L2T2, km_L2T2
L(r,t!1,mod~1)@Cell.T2(l!1)@O2M -> T2(l)@O2M + L(r,t,mod~1)@Organelle_2 k_L2T2trans
end reaction rules

generate_network({overwrite=>1})
writeSBML()  
simulate({method=>"ode",t_start=>0,t_end=>0.07,n_steps=>20})  